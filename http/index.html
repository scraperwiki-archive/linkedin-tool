<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">
    <title>LinkedIn creeper tool</title>
    <meta http-equiv="cleartype" content="on">
    <link rel="stylesheet" href="vendor/css/bootstrap.min.css">
    <link rel="stylesheet" href="//x.scraperwiki.com/style/scraperwiki.css">
    <style type="text/css" rel="stylesheet">
    body {
      padding: 20px;
    }
    textarea {
      height: 150px;
      width: 250px;
    }
    .last span {
      display: inline-block;
      margin-left: 10px;
      font-size: 14px;
      line-height: 20px;
    }
    </style>
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.8/jquery.min.js"></script>

    <script src="https://x.scraperwiki.com/js/scraperwiki.js"></script>
    <script src="vendor/js/bootstrap.min.js"></script>
    <script src="vendor/js/jquery-io.js"></script>
    <script src="http://coffeescript.org/extras/coffee-script.js"></script>
  </head>
  <body>
     <form>
         <p>
           Paste a list of names in here. One name per line please.
         </p>
         <p>
           <textarea></textarea>
         </p>
         <p class="last">
           <button class="btn btn-primary" type="button" id="authenticate">Import</button>
         </p>
     </form>
    <div>
    </div>
    <script type="text/coffeescript">

requestAccessToken = ->
  grant_type: authorization_code

getClientIdAndSecret = (callback) ->
  scraperwiki.exec 'cat ~/tool/oauth_keys.json', (data) ->
    keys = JSON.parse data
    callback null, {id: keys.apiKey, secret: keys.secretKey}

populateTextArea = ->
  scraperwiki.sql "select name from source", (data) ->
    list = (p.name for p in data)
    text = list.join('\n')
    $('textarea').val(text)

handleAjaxError = (jqXHR, textStatus, errorThrown) ->
  console.warn textStatus, errorThrown

runSeeker = ->
  scraperwiki.exec "tool/code/seeker.py --limit 100", (data) ->
    console.log data
    if data != ""
      scraperwiki.alert "Unknown error!", data, true
    else
      $('#authenticate').removeClass('loading').text('Import')
      scraperwiki.tool.redirect "/dataset/#{scraperwiki.box}"
  , handleAjaxError

alertAuthenticated = ->
  $('.last').append('<span><i class="icon-ok"></i> Authenticated</span>')

importNames = (callback) ->
  scraperwiki.exec "cat 2>/dev/null access_token.json", (data) ->
    if not data.length
      # Don't have access_tokens yet, so start the process
      # of getting them.
      getClientIdAndSecret (err, keys) ->
        scraperwiki.tool.getURL (url) ->
          params =
            response_type: 'code'
            client_id: keys.id
            scope: 'r_network'
            state: 'abc'
            redirect_uri: url
          qs = $.io.object(params).query()
          target = "https://www.linkedin.com/uas/oauth2/authorization?#{qs}"
          scraperwiki.tool.redirect target
    else
      alertAuthenticated()
      runSeeker()

$ ->
  populateTextArea()

  scraperwiki.tool.getURL (url) ->
    qs = url.match(/[?](.*)/)
    if qs?
      # Redirected from LinkedIn
      qs = qs[1]
      console.log "GOT QS #{qs}"
      params = $.io.query(qs).object()
      if params.code and params.state
        getClientIdAndSecret (err, keys) ->
          console.log "GOT CODE #{qs}"
          # LinkedIn have redirected back here.
          redirect_url = url.match(/(.*?)[?]/)[1]
          access_params =
            grant_type: 'authorization_code'
            code: params.code
            redirect_uri: redirect_url
            client_id: keys.id
            client_secret: keys.secret
          access_qs = $.io.object(access_params).query()
          target = "https://api.linkedin.com/uas/oauth2/accessToken?#{access_qs}"
          scraperwiki.exec "curl #{scraperwiki.shellEscape(target)} > access_token.json", ->
            alertAuthenticated()
            runSeeker()
            # TODO: cron job, show loading button
      else if params.error and params.state
        console.log "GOT ERROR #{qs}"
        # Some sort of error (from LinkedIn).
        scraperwiki.alert params.error, params.description, true
      else
        scraperwiki.alert "Mysterious params", "WAT?", true
    else
      console.log "GOT no query parameters"
      # No query params, so we haven't been redirected here.
      # Must be viewing the tool settings, possibly having
      # just been installed.

  $('#authenticate').on 'click', ->
    $(@).addClass('loading').text('Importing')
    escapedNames = scraperwiki.shellEscape $('textarea').val()
    scraperwiki.exec "echo #{escapedNames} | python tool/code/namestodb.py", (data) ->
      if data != ""
        scraperwiki.alert "Unknown error!", data, true
      importNames()

        </script>

      </body>
</html>
